plugins {
    id 'groovy'
    id 'java'
    id 'pl.allegro.tech.build.axion-release' version '1.9.3'
}

group 'com.rundeck.plugin'
ext.pluginClassNames = 'com.rundeck.plugins.ScriptNodeEnhancer'
ext.pluginName = 'Nodes Enhancer Script'
ext.pluginDescription = 'add attributes from a script'

scmVersion {
    tag {
        //defines release tags like "v1.2.3"
        prefix = 'v'
        versionSeparator = ''
    }
    ignoreUncommittedChanges = false
}
project.version = scmVersion.version

sourceCompatibility = 1.8

repositories {
    mavenCentral()
}

configurations{
    //declare custom pluginLibs configuration to include only libs for this plugin
    pluginLibs

    //declare compile to extend from pluginLibs so it inherits the dependencies
    compile{
        extendsFrom pluginLibs
    }
}

dependencies {
    compile 'org.codehaus.groovy:groovy-all:2.3.11'
    compile 'org.rundeck:rundeck-core:3.0.14-20190221'
    pluginLibs group: 'org.codehaus.groovy.modules.http-builder', name: 'http-builder', version: '0.7.1'
    pluginLibs group: 'com.google.code.gson', name: 'gson', version: '2.8.2'


    testCompile group: 'junit', name: 'junit', version: '4.12'
}

// task to copy plugin libs to output/lib dir
task copyToLib(type: Copy) {
    into "$buildDir/output/lib"
    from configurations.pluginLibs
}


jar {
    from "$buildDir/output"
    manifest {
        def libList = configurations.pluginLibs.collect{'lib/' + it.name}.join(' ')

        attributes 'Rundeck-Plugin-Version': '2.0',
                'Rundeck-Plugin-Archive': 'true',
                'Rundeck-Plugin-File-Version': version,
                'Rundeck-Plugin-Author': 'Luis Toledo',
                'Rundeck-Plugin-URL': 'http://rundeck.com',
                'Rundeck-Plugin-Date': new Date().format("yyyy-MM-dd'T'HH:mm:ssX"),
                'Rundeck-Plugin-Classnames': pluginClassNames,
                'Rundeck-Plugin-Name': pluginName,
                'Rundeck-Plugin-Description': pluginDescription,
                'Rundeck-Plugin-Rundeck-Compatibility-Version': '3.0.14+',
                'Rundeck-Plugin-Libs': "${libList}"


    }
    dependsOn(copyToLib)

}

//set jar task to depend on copyToLib
jar.dependsOn(copyToLib)
